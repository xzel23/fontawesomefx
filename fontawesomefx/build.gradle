buildscript {
  repositories {
    mavenLocal()
    //maven { url  "https://dl.bintray.com/dua3/public" }
  }  
  dependencies {
    classpath group: 'com.dua3.gradle.jpms', name: 'JpmsGradlePlugin', version: '0.2.2'
  }
}

plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'base'
apply from: 'deploy.gradle'

subprojects {
    apply plugin: 'com.dua3.gradle.jpms'
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "developer-id"
            name "developer-name"
            email "dev@d.com"
        }
    }

    scm {
        url "https://github.com/yourgithubaccount/example"
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: "java"
    apply plugin: "signing"

    signing {
        sign configurations.archives
    }

    group = artifactGroupId
    sourceCompatibility = '8'
    targetCompatibility = '8'
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    bintray {
        user = findProperty('BINTRAY_USER') ?: 'NONE'
        key = findProperty('BINTRAY_KEY') ?: 'NONE'

        dryRun = false
        override = false
        publish = false

        pkg {
            repo = 'public'
            name = artifactName
            userOrg = artifactUserOrg
            licenses = artifactLicenses
            vcsUrl = artifactGitUrl
            publications = artifactPublications
            websiteUrl = artifactWebSiteUrl
            issueTrackerUrl = artifactIssueTrackerUrl
            publicDownloadNumbers = true
            version {
                gpg {
                    sign = true
                    passphrase = System.getenv('SIGNING_PASSWORD')
                }
                name = '9.1.2-jdk8-1'
                desc = 'FontAwesomeFX 9.1.2-jdk8-1'
                vcsTag = 'fontawesomefx-9.1.2-jdk8-1'
            }
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', artifactDescription)
                    root.appendNode('name', artifactName)
                    root.appendNode('url', artifactWebSiteUrl)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    afterEvaluate {
        repositories {
            jcenter()
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
